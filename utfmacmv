#!/usr/bin/perl

use strict;
use warnings;

use Encode qw/decode_utf8 encode_utf8/;
use Unicode::Normalize;

use Getopt::Long;

my ( $verbose, $noclobber );

Getopt::Long::Configure('no_ignore_case');
unless (
    GetOptions(
        'help|h'      => \&showusage,
        'verbose|v'   => \$verbose,
        'noclobber|n' => \$noclobber,
    )
  )
{
    showusage();
    die "incorrect option\n";
}

while ( my $filename = shift(@ARGV) ) {
    my $newname = encode_utf8( NFC( decode_utf8 $filename) );
    if ( $filename eq $newname ) {
        print "Same target name $filename\n";
		next;
    } else {
        if ( $noclobber and -f $newname ) {
            print "Skipping existing file '$newname'\n" if $verbose;
            next;
        }
    print "Renaming '$filename' to '$newname'\n" if $verbose;
    rename( $filename, $newname ) or warn "error renaming '$filename': $!\n";
    }
}

sub showusage {

    print <<EOF
$0 
Rename files whose name is utf8-mac encoded (UTF8 NFD) to plain utf8 (UTF8 NFC)

Options:
	-h, --help		This help
	-n, --noclobber 	Don't overwrite existing files

EOF
      ;
    exit;
}
